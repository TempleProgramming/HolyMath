
asm
{
_VEC3::
	PUSH		RBP
	MOV			RBP, RSP

	MOV			RAX, SF_ARG4[RBP]	// = *dest

	CVTSD2SS	XMM0, SF_ARG1[RBP]
	MOVSS		[RAX], XMM0

	CVTSD2SS	XMM0, SF_ARG2[RBP]
	MOVSS		4[RAX], XMM0

	CVTSD2SS	XMM0, SF_ARG3[RBP]
	MOVSS		8[RAX], XMM0

	POP			RBP
	RET1		32
}
/**
	@ingroup Math
	@brief Initialize members of a vector with double-precision floats.

	@param[in]     x	X component.
	@param[in]     y	Y component.
	@param[in]     z	Z component.
	@param[in,out] dest	Destination
*/
_extern _VEC3 U0 Vec3(F64 x, F64 y, F64 z, CVec3 *dest);

/**
	@ingroup Math
	@brief Print members of a vector.

	@param[in] v	Vector
*/
U0 Vec3Print(CVec3 *v)
{
	U8 reg R15 str = "%n\t%n\t%n\n\n";
asm
{
	MOV			RAX, SF_ARG1[RBP]	// = *v
	SUB			RSP, 24

	CVTSS2SD	XMM0, 8[RAX]
	MOVSD_SSE	16[RSP], XMM0

	CVTSS2SD	XMM0, 4[RAX]
	MOVSD_SSE	8[RSP], XMM0

	CVTSS2SD	XMM0, [RAX]
	MOVSD_SSE	[RSP], XMM0

	PUSH		3					// # of varargs
	PUSH		R15
	CALL		&Print
	ADD			RSP, 40
}
}

/**
	@ingroup Math
	@brief Copy all members of a vector to destination.

	@param[in]     src	Source
	@param[in,out] dest	Destination 
*/
_extern _VEC4_COPY U0 Vec3Copy(CVec3 *src, CVec3 *dest);

asm
{
_VEC3_IS_EQUAL::
	PUSH		RBP
	MOV			RBP, RSP

	MOV			RAX, SF_ARG1[RBP]	// = *a
	MOVAPS		XMM0, [RAX]
	MOV			RAX, SF_ARG2[RBP]	// = *b
	MOVAPS		XMM1, [RAX]
	CMPPS		XMM0, XMM1, 0		// CMPEQPS

	PMOVMSKB	RAX, XMM0
	AND			RAX, 0xFFF
	CMP			RAX, 0xFFF
	JNZ			_is_not_equal
	MOV			RAX, 1
	JMP			_return
_is_not_equal:
	MOV			RAX, 0

_return:
	POP		RBP
	RET1	16
}
/**
	@ingroup Math
	@brief Checks if two vectors are equal.

	@param[in] a	Vector 1
	@param[in] b	Vector 2
*/
_extern _VEC3_IS_EQUAL Bool Vec3IsEqual(CVec3 *a, CVec3 *b);

/**
	@ingroup Math
	@brief Sum of two vectors.

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@param[in,out] dest	Destination
*/
_extern _VEC4_ADD U0 Vec3Add(CVec3 *a, CVec3 *b, CVec3 *dest);

/**
	@ingroup Math
	@brief Add a scalar to a vector.

	@param[in]     v	Vector
	@param[in]     s	Scalar
	@param[in,out] dest	Destination
*/
_extern _VEC4_ADDS U0 Vec3AddS(CVec3 *v, I32 *s, CVec3 *dest);

/**
	@ingroup Math
	@brief Difference of two vectors.

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@param[in,out] dest	Destination
*/
_extern _VEC4_SUB U0 Vec3Sub(CVec3 *a, CVec3 *b, CVec3 *dest);

/**
	@ingroup Math
	@brief Subtract a scalar from a vector.

	@param[in]     v	Vector
	@param[in]     s	Scalar
	@param[in,out] dest	Destination
*/
_extern _VEC4_SUBS U0 Vec3SubS(CVec3 *v, I32 *s, CVec3 *dest);

/**
	@ingroup Math
	@brief Product  of two vectors (element multiplication).

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@param[in,out] dest	Destination
*/
_extern _VEC4_MUL U0 Vec3Mul(CVec3 *a, CVec3 *b, CVec3 *dest);

/**
	@ingroup Math
	@brief Scale a vector by a scalar.

	@param[in]     v	Vector
	@param[in]     s	Scalar
	@param[in,out] dest	Destination
*/
_extern _VEC4_MULS U0 Vec3MulS(CVec3 *v, I32 *s, CVec3 *dest);

/**
	@ingroup Math
	@brief Quotient of two vectors.

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@param[in,out] dest	Destination
*/
_extern _VEC4_DIV U0 Vec3Div(CVec3 *a, CVec3 *b, CVec3 *dest);

/**
	@ingroup Math
	@brief Divide a vector by a scalar.

	@param[in]     v	Vector
	@param[in]     s	Scalar
	@param[in,out] dest	Destination
*/
_extern _VEC4_DIVS U0 Vec3DivS(CVec3 *v, I32 *s, CVec3 *dest);

/**
	@ingroup Math
	@brief Min of two vectors (element-wise).

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@param[in,out] dest	Destination
*/
_extern _VEC4_MIN U0 Vec3Min(CVec3 *a, CVec3 *b, CVec3 *dest);

/**
	@ingroup Math
	@brief Max of two vectors (element-wise).

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@param[in,out] dest	Destination
*/
_extern _VEC4_MAX U0 Vec3Max(CVec3 *a, CVec3 *b, CVec3 *dest);

/**
	@ingroup Math
	@brief Negate a vector (elements = -elements).

	@param[in]     v	Vector
	@param[in,out] dest	Destination
*/
_extern _VEC4_NEGATE U0 Vec3Negate(CVec3 *v, CVec3 *dest);

asm
{
_VEC3_NORMALIZE::
	PUSH		RBP
	MOV			RBP, RSP

	MOV			RAX, SF_ARG1[RBP]
	MOVAPS		XMM0, [RAX]
	MOVAPS		XMM2, XMM0
	MULPS		XMM0, XMM2

	MOVHLPS		XMM1, XMM0
	ADDSS		XMM1, XMM0
	SHUFPS		XMM0, XMM0, 0x55	// (1, 1, 1, 1)
	ADDSS		XMM0, XMM1
	SQRTSS		XMM0, XMM0
	SHUFPS		XMM0, XMM0, 0x00	// (0, 0, 0, 0)
	DIVPS		XMM2, XMM0

	MOV			RAX, SF_ARG2[RBP]
	MOVAPS		[RAX], XMM2

	POP			RBP
	RET1		16
}
/**
	@ingroup Math
	@brief Normalize a vector (length = 1.0).

	@param[in]     v	Vector
	@param[in,out] dest	Destination
*/
_extern _VEC3_NORMALIZE U0 Vec3Normalize(CVec3 *v, CVec3 *dest);

asm
{
_VEC3_DOT::
	PUSH		RBP
	MOV			RBP, RSP

	MOV			RAX, SF_ARG1[RBP]
	MOVAPS		XMM0, [RAX]
	MOV			RAX, SF_ARG2[RBP]
	MOVAPS		XMM1, [RAX]
	MULPS		XMM0, XMM1

	MOVHLPS		XMM1, XMM0
	ADDSS		XMM1, XMM0
	SHUFPS		XMM0, XMM0, 0x55	// (1, 1, 1, 1)
	ADDSS		XMM0, XMM1
	MOVQ		RAX, XMM0

	POP			RBP
	RET1		16
}
/**
	@ingroup Math
	@brief Dot product of two vectors.

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@return				Dot product.
*/
_extern _VEC3_DOT I32 Vec3Dot(CVec3 *a, CVec3 *b);

asm
{
_VEC3_CROSS::
	PUSH		RBP
	MOV			RBP, RSP

	MOV			RAX, SF_ARG1[RBP]
	MOVAPS		XMM0, [RAX]
	MOVAPS		XMM1, XMM0
	MOV			RAX, SF_ARG2[RBP]
	MOVAPS		XMM2, [RAX]
	MOVAPS		XMM3, XMM2

	SHUFPS		XMM0, XMM0, 0xC9	// (1, 2, 0, 3)  [Ay Az Ax]
	SHUFPS		XMM1, XMM1, 0xD2	// (2, 0, 1, 3)  [Az Ax Ay]
	SHUFPS		XMM2, XMM2, 0xD2	// (2, 0, 1, 3)  [Bz Bx By]
	SHUFPS		XMM3, XMM3, 0xC9	// (1, 2, 0, 3)  [By Bz Bx]

	MULPS		XMM0, XMM2
	MULPS		XMM1, XMM3
	SUBPS		XMM0, XMM1

	MOV			RAX, SF_ARG3[RBP]
	MOVAPS		[RAX], XMM0

	POP			RBP
	RET1		24
}
/**
	@ingroup Math
	@brief Cross product of two vectors.

	@param[in]     a	Vector 1
	@param[in]     b	Vector 2
	@param[in,out] dest	Destination
*/
_extern _VEC3_CROSS U0 Vec3Cross(CVec3 *a, CVec3 *b, CVec3 *dest);












